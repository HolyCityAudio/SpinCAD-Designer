@name MN3011
@color "0x6060c4"
@audioInput input Input 
@audioInput feedback 'Feedback Input'
@audioOutput mix 'Mix Out'
@audioOutput tap6reg 'Tap 6 Out' 
@controlInput cIn1 'Delay Time' 
@controlInput fbk Feedback

equ inputGain 0.5
@sliderLabel inputGain 'Input Gain'  -24 0 -6 1.0 1 DBLEVEL

equ fbkGain 0.5
@sliderLabel fbkGain 'Feedback Gain' -24 0 -6 1.0 1 DBLEVEL 

// total allocated memory buffer for this delay
// create a Control Panel with a Slider Label
equ delayLength 32767 
@sliderLabel delayLength 'Delay Time' 0 32767 16384 1 0 LENGTHTOTIME
		
// tap 1 length in % (control panel value) 
// 	'equ' ename = ID value = SPINDOUBLE (controlType = ID controlName = ID max = SPINDOUBLE multiplier = SPINDOUBLE)?;

// tap1 = 396/3328
equ tap1Ratio 0.118990385 
equ tap1Gain 0.5
@sliderLabel tap1Gain 'Tap 1 Gain'  -24 0 -6 1.0 1 DBLEVEL
  
// tap 2 = 662/3328
equ tap2Ratio 0.198918269 
equ tap2Gain 0.5
@sliderLabel tap2Gain 'Tap 2 Gain'  -24 0 -6 1.0 1 DBLEVEL

// tap 3 
equ tap3Ratio 0.358774038 
equ tap3Gain 0.5
@sliderLabel tap3Gain 'Tap 3 Gain'  -24 0 -6 1.0 1 DBLEVEL

equ tap4Ratio 0.518629808
equ tap4Gain 0.5
@sliderLabel tap4Gain 'Tap 4 Gain'  -24 0 -6 1.0 1 DBLEVEL
  
equ tap5Ratio 0.838341346 
equ tap5Gain 0.5
@sliderLabel tap5Gain 'Tap 5 Gain'  -24 0 -6 1.0 1 DBLEVEL
  
equ tap6Ratio 1.0 
equ tap6Gain 0.5
@sliderLabel tap6Gain 'Tap 6 Gain'  -24 0 -6 1.0 1 DBLEVEL

@isPinConnected 'Tap 6 Out'
equ tap6reg REG12
@endif
 
equ delayOffset -1

// establish the base address for this module
@getBaseAddress 
// then allocate the buffer
mem mn3011delay delayLength 

// input connection must be there for any code to be generated
@isPinConnected Input

equ   mix  reg0      
// read the input and write to base of delay line
@isPinConnected 'Feedback Input'
rdax feedback,fbkGain
@isPinConnected Feedback
mulx fbk
@endif
@endif
rdax input,inputGain

wra mn3011delay, 0.0

// tap 1 -----------------------------------------------------------

@isPinConnected 'Delay Time'
equ max reg1

clr
or $7FFF00
mulx cIn1
// max holds the value of the POT scaled by max ADDR_PTR
// we re-use this for each tap
wrax max,1.0

// tap 6 -----------------------------------------------------------
@getDelayScaleControl tap6Ratio delayLength delayOffset
wrax ADDR_PTR, 0
rmpa tap6Gain
wrax mix, 0.0

// if tap6 out is connected, save tap6 value alone (typically for feedback)
// doing this tap first saves a few instructions.
// tap6 output will not  be scaled by the gain setting

@isPinConnected 'Tap 6 Out'
rmpa 1.0
wrax tap6reg, 0.0
@endif
// tap 1 -----------------------------------------------------------

rdax max, 1.0
@getDelayScaleControl tap1Ratio delayLength delayOffset 
wrax ADDR_PTR, 0
rmpa tap1Gain
rdax mix, 1.0
wrax mix, 0.0 

// tap 2 -----------------------------------------------------------
rdax max, 1.0

@getDelayScaleControl tap2Ratio delayLength delayOffset 
wrax ADDR_PTR, 0
rmpa tap2Gain
rdax mix, 1.0
wrax mix, 0.0

// tap 3 -----------------------------------------------------------
rdax max, 1.0

@getDelayScaleControl tap3Ratio delayLength delayOffset
wrax ADDR_PTR, 0
rmpa tap3Gain
rdax mix, 1.0
wrax mix, 0.0

// tap 4 -----------------------------------------------------------
rdax max, 1.0

@getDelayScaleControl tap4Ratio delayLength delayOffset
wrax ADDR_PTR, 0
rmpa tap4Gain
rdax mix, 1.0
wrax mix, 0.0

// tap 5 -----------------------------------------------------------
rdax max, 1.0

@getDelayScaleControl tap5Ratio delayLength delayOffset
wrax ADDR_PTR, 0
rmpa tap5Gain
rdax mix, 1.0

;;wrax mix, 0.0 

// fixed delay spacing below 
@else
offset ratio 1   

// as ACC=0 at this point, do tap6 read first, so if the tap 6 output is
// connected, we can save that register directly instead of having to
// save the mix first

@getSamplesFromRatio ratio tap6Ratio delayLength 
@isPinConnected 'Tap 6 Out' 
rda mn3011delay+ratio, 1.0
wrax tap6reg, 0.0
@endif
rda mn3011delay+ratio, tap6Gain

@getSamplesFromRatio ratio tap1Ratio delayLength 
rda mn3011delay+ratio, tap1Gain

@getSamplesFromRatio ratio tap2Ratio delayLength 
rda mn3011delay+ratio, tap2Gain

@getSamplesFromRatio ratio tap3Ratio delayLength 
rda mn3011delay+ratio, tap3Gain

@getSamplesFromRatio ratio tap4Ratio delayLength 
rda mn3011delay+ratio, tap4Gain

@getSamplesFromRatio ratio tap5Ratio delayLength 
rda mn3011delay+ratio, tap5Gain
@endif

wrax mix, 0.0
@setOutputPin 'Mix Out' mix

@setOutputPin 'Tap 6 Out' tap6reg

@endif


