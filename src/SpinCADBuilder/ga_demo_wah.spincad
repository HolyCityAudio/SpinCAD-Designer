@name Wah
@audioInput adcl Input_Left 
@audioInput adcr Input_Right 
@audioOutput dacl Audio_Output_1
@audioOutput dacr Audio_Output_2

@controlInput input0 Reverb_Level 
@controlInput input1 Wah_Sens
@controlInput input2 Wah_Level_Q 

;GA_DEMO	Wah

;Pot0 = Reverb level (post wah)
;Pot1 = Sensitivity
;pot2 = Effect level / filter Q

;memory declarations:

mem	ap1	334
mem	ap2	556
mem	ap3	871

mem	lap1a	808
mem	lap1b	1934
mem	d1	2489

mem	lap2a	1016
mem	lap2b	1787
mem	d2	2287

;register equates:

equ	mono	reg0
equ	apout	reg1
equ	lp1	reg2
equ	lp2	reg3
equ	revout	reg4
equ	wahout	reg5
equ	wf1	reg6
equ	wf2	reg7
equ	wf	reg8
equ	wq	reg9
equ	temp	reg10
equ	avg	reg11
equ	lavg	reg12
equ	ffil	reg13
equ	bypass	reg14
equ output1 reg15
equ output2 reg16

;declare constants:

equ	kap	0.6	;all pass coefficient
equ	krt	0.55	;reverb time
equ	krf	0.5	;reverb lpf freq
equ	krs	-0.6	;reverb lpf shelf

;clear registers and initialize LFOs:

@isPinConnected Input_Left
skp	run,endclr
wrax	lp1,0
wrax	lp2,0
wrax	wf1,0
wrax	wf2,0
wlds	sin0,12,100
endclr:

;sum inputs to mono:

rdax	adcl,0.5
rdax	adcr,0.5
wrax	mono,0	

;do reverb and put result in revout (after pot0 control):

rdax	wahout,0.5
rda	ap1#,kap
wrap	ap1,-kap
rda	ap2#,kap
wrap	ap2,-kap
rda	ap3#,kap
wrap	ap3,-kap
wrax	apout,0

rda	d2#,krt
rdax	apout,1
rda	lap1a#,kap
wrap	lap1a,-kap
rda	lap1b#,kap
wrap	lap1b,-kap
rdfx	lp1,krf
wrlx	lp1,krs
wra	d1,0

rda	d1#,krt
rdax	apout,1
rda	lap2a#,kap
wrap	lap2a,-kap
rda	lap2b#,kap
wrap	lap2b,-kap
rdfx	lp2,krf
wrlx	lp2,krs
wra	d2,1.99

@isPinConnected Reverb_Level
rda	d1,1.99
mulx	input0
mulx	input0
@else
rda d1, 0.5
@endif

wrax	revout,0

;smooth reverb:

cho rda,sin0,sin|reg|compc, lap1b+100  
cho rda,sin0,sin,lap1b+101
wra	lap1b+200,0
cho rda,sin0,sin|reg|compc,lap2b+100
cho rda,sin0,sin,lap2b+101
wra	lap2b+200,0

;Now do wah, a 2 pole LPF, peaking.
;begin by getting control level into detector:

rdax	mono,1		;get input
absa			;absolute value
rdfx	avg,0.01		;average input level
wrax	avg,0		;write avg level, pass on
rdax	lavg,0.001
sof	-0.01,0	
rdax	lavg,1	
wrax	temp,0
rdax	avg,1
maxx	temp,1		;filter a long average
wrax	lavg,1

;now set up a means by which the sensitivity control can affect the filter frequency:

; rdax	lavg,1
sof 	1,0.002		;never let lavg go to zero
log	1,0
wrax	temp,0
rdax	avg,1
log	1,0

@isPinConnected Wah_Sens 
rdax	temp,-1
mulx 	input1
@else
rdax temp, -0.5
@endif

exp	1,0
rdfx	ffil,0.0005
wrax	ffil,1		
sof	0.7,0.02		;limit frequency range
wrax	wf,0

;get wfq value from pot2:

@isPinConnected Wah_Level_Q
rdax	input2,1
sof	-0.2,0.25	;make 0.3 to 0.05
@else
sof 0.0, 0.2
@endif

wrax	wq,0		;write Q value (pos number)

;do filter:

rdax	wf1,1
mulx	wf
rdax	wf2,1
wrax	wf2,-1
rdax	mono,1
wrax	temp,0		;save signal
rdax	wf1,-1
mulx	wq
rdax	temp,1
mulx	wf
rdax	wf1,1
wrax	wf1,0		;finished with biquad filter.

;derive bypass signal from pot2:

@isPinConnected Wah_Level_Q
rdax	input2,1
@else
sof 0.0, 0.8
@endif

rdax	bypass,0.9	;recursive gain
wrax	bypass,0

;now combine outputs to make a wahout signal 

rdax	mono,-1
rdax	wf2,1
mulx	bypass
rdax	mono,1
wrax	wahout,1
rdax	revout,1
; sof	1,0.02
wrax	output1,1
; sof	1,-0.04
wrax	output2,0
@setOutputPin "Audio_Output_1" output1
@setOutputPin "Audio_Output_2" output2
@endif