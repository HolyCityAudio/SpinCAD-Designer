@name "Small Reverb"  
@color "0x7100fc"
@audioInput input1 Input_Left 
@audioInput input2 Input_Right 
@audioOutput outputL Output_Left
@audioOutput outputR Output_Right 

@controlInput revTime Reverb_Time

equ gain 0.5 
@sliderLabel gain 'Input Gain' -18 0 -6 1.0 1 DBLEVEL 

equ rtMax 0.9

;constants:  ---- these should have control panel sliders

equ   kiap   0.5  ; input all pass filter
@sliderLabel kiap 'Input All Pass:' -0.90 0.90 0.5 100.0 2 

equ ap1length 125
@sliderLabel ap1length 'Input AP1 (msec)'  20 2000 125 1 1 LENGTHTOTIME

equ ap2length 250
@sliderLabel ap2length 'Input AP2 (msec)'  20 2000 250 1 1 LENGTHTOTIME

equ ap3length 750
@sliderLabel ap3length 'Input AP3 (msec)'  20 2000 750 1 1 LENGTHTOTIME

equ ap4length 1500
@sliderLabel ap4length 'Input AP4 (msec)'  20 2000 1500 1 1 LENGTHTOTIME

; block control parameters from control panel

equ   klap   0.6  ;  delay ring all pass filter
@sliderLabel klap 'Loop All Pass' -0.90 0.90 0.6 100.0 2 

equ lap1length 3800
@sliderLabel lap1length 'Loop AP1 (msec)'  3500 5000 3800 1 0 LENGTHTOTIME

equ del1length 6250
@sliderLabel del1length 'Loop Delay 1 (msec)'  5000 9000 7250 1 0 LENGTHTOTIME

equ lap2length 4200
@sliderLabel lap2length 'Loop AP2 (msec)' 3500 5000 4200 1 0 LENGTHTOTIME

equ del2length 7250
@sliderLabel del2length 'Loop Delay 2 (msec)'  5000 9000 7250  1 0 LENGTHTOTIME

; equ   kfl   0.4  ; low pass filter
; @sliderLabel kfl 'Low Pass' 500 5000 800.0 100.0 1 LOGFREQ

; equ   kfh   0.01  ; high pass filter
; @sliderLabel kfh 'High Pass' 40 1000 400.0 100.0 1 LOGFREQ

@isPinConnected Input_Left

equ    temp   reg0  ; locations for filtering
equ   iapout   reg10 ; holding register for all pass output
equ outputL reg12
equ outputR reg13
equ   hpf3   reg3
equ   lpf3   reg7
equ krt reg2
equ	apout	reg1	;holding reg input AP signal

@getBaseAddress 

;prepare decay time pot:
@isPinConnected Reverb_Time
rdax   revTime,rtMax
@else
sof   0,rtMax
@endif
wrax   krt,0         ;rt ranges 0.3 to 0.85

;sample reverb program for FV-1
;minimize number of delays and ops.
;4 aps driving 2 AP-delay loops
;drive both loop elements, take output from each
;no pot controls
;output is full reverb, not mixed
;22 operations (of 128)

;input all passes (4)

mem	api1	ap1length
mem	api2	ap2length
mem	api3	ap3length
mem	api4	ap4length
	
mem	ap1	lap1length
mem	del1 del1length	; input = left output

mem	ap2	lap2length
mem	del2 del2length	; input = right output

rdax	input1,gain	;read inputs, 
@isPinConnected Input_Right
rdax	input2,gain	;attenuate, sum and
@endif
 
rda	api1#,kiap	;do 4 APs
wrap	api1,-kiap
rda	api2#,kiap
wrap	api2,-kiap
rda	api3#,kiap
wrap	api3,-kiap
rda	api4#,kiap
wrap	api4,-kiap
wrax	apout,1		;write to min, keep in ACC

; first loop apd: AP'd input in ACC
rda	del2#, rtMax	;read del2, scale by rtMax
@isPinConnected Reverb_Time
mulx revTime
@endif

rda	ap1#, -klap	;do loop ap
wrap	ap1,klap
wra	del1, 1.99	;write delay, x2 for dac out
wrax	outputL, 0

;second loop apd:

rdax	apout,1		;get input signal again
rda	del1#, rtMax	;as above, to other side of loop
@isPinConnected Reverb_Time
mulx revTime
@endif
rda	ap2#,klap
wrap	ap2,-klap
wra	del2, 1.99
wrax	outputR, 0

@setOutputPin Output_Left outputL
@setOutputPin Output_Right outputR

@endif
